config {
  type: "operations",
  dependencies: "df_unnest_stg_present_ar"
}
SET @@query_label =
  "step:delete_duplicate_assessment, " ||
  "batch_id: ${dataform.projectConfig.vars.batch_id}";

delete from dataform.df_unnest_stg_present_ar usra_o
 where usra_o.stg_id in  
    (
      /*
      Find all RAFT assessments where total responses count is same as not_answered results count
      Delete them from unnested table such that bad data do not go to fact_ar and read_velocity 
      denominator count_present_responses remains saner.
      */
      with cte as 
          (
            select 
              usra.stg_id,
              usra.assessment_id,
              usra.question_id,
              count(1) as count_total_responses,
              sum ( CASE  
                      when result = 'not_answered' then 1 else 0 
                    end ) as count_not_answered_responses

          from `dataform.df_unnest_stg_present_ar` usra
          join `readanalytics.assessment` a on a.id = usra.assessment_id
          where a.assessment_mode_id = 1 -- ONLY RAFT to be considered for delete 
          group by  
            usra.stg_id,
            usra.assessment_id,
            usra.question_id
          having count_total_responses = count_not_answered_responses 
    )  select cte.stg_id 
       from cte
  );
  
  