config {
  type: "table",
  dependencies: ["begin_assessment_load"]
}
pre_operations {
    SET @@query_label =
      "step:df_sc_asmt_stu, " ||
      "batch_id: ${dataform.projectConfig.vars.batch_id}";
}

WITH CTE AS(SELECT  
    -- primary keys
      fact.assessment_id
    , fact.study_circle_id
    , fact.student_id
   
    -- count measures
    , IFNULL(SUM(count_hand_raises),0) AS count_hand_raises
    , SUM(sum_response_velocity) AS sum_response_velocity
    , SUM(count_present_responses) AS count_present_responses
    
    -- array fields
    , ARRAY_AGG(STRUCT(
        fact.question_id AS q_id,
        fatq.question_sequence as q_sequence,
        fact.result as result
      )) AS question_wise_results

    -- audit fields
    , MAX(ts) AS ts

FROM readanalytics.fact_ar fact
LEFT JOIN readanalytics.fact_asmt_q fatq ON fact.question_id = fatq.question_id AND fact.assessment_id = fatq.assessment_id
-- WHERE fact.ts > '${dataform.projectConfig.vars.batch_start_ts}' AND fact.ts <= '${dataform.projectConfig.vars.batch_end_ts}'
GROUP BY
    -- primary keys
    assessment_id,
    study_circle_id,
    student_id)
SELECT 
 -- primary keys
      CTE.assessment_id
    , CTE.study_circle_id
    , CTE.student_id
   
    -- count measures
    , CTE.count_hand_raises
    , CTE.sum_response_velocity
    , CTE.count_present_responses
    
    -- array fields
    , CTE.question_wise_results

    -- array assessment
    , ARRAY_AGG(STRUCT(CTE.assessment_id)) OVER (PARTITION BY CTE.study_circle_id,a.topic_id,map.id,CTE.student_id) AS arr_assessments

    -- audit fields
    , ts

FROM CTE
LEFT JOIN readanalytics.assessment a ON a.id =  CTE.assessment_id
LEFT JOIN `readanalytics.master_study_circle` msc ON msc.id = CTE.study_circle_id
LEFT JOIN `readanalytics.master_course` mc ON mc.id = msc.ref_course_id
LEFT JOIN `readanalytics.master_academic_period` map ON map.board_id = mc.board_id AND a.assessment_date BETWEEN map.start_date AND map.end_date