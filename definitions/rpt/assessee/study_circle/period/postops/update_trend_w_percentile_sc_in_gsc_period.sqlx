config {
  type: "operations",
  dependencies: ["update_window_measures_history_sc_period"]
}
SET @@query_label =
  "step:update_trend_w_percentile_sc_in_gsc_period, " ||
  "batch_id: ${dataform.projectConfig.vars.batch_id}";

UPDATE readanalytics.rpt_sc_rac_period rper
SET rper.trend_w_percentile_sc_in_gsc = trend.trend_w_percentile_sc_in_gsc
FROM(
SELECT 
    rper.assessment_academic_period_id
    , rper.study_circle_id
    , rper.dim_ra_cube_id
    , (
      SELECT 
        CASE 
          WHEN 
            max(latest) IS NULL 
            OR max(second_latest) IS NULL 
            OR max(third_latest) IS NULL 
            OR max(fourth_latest) IS NULL 
          THEN 'INDETERMINATE'
          WHEN 
            (max(latest) + max(second_latest)) > 1.1 * (max(third_latest) + max(fourth_latest)) 
          THEN 'IMPROVING'
          WHEN (max(latest) + max(second_latest)) < 0.9 * (max(third_latest) + max(fourth_latest)) 
          THEN 'DECLINING'
          ELSE 'STABLE'
        END AS trend
      FROM (
        SELECT
          CASE WHEN rnum = 0 THEN hist.percentile_gsc END AS latest 
          , CASE WHEN rnum = 1 THEN hist.percentile_gsc END AS second_latest 
          , CASE WHEN rnum = 2 THEN hist.percentile_gsc END AS third_latest 
          , CASE WHEN rnum = 3 THEN hist.percentile_gsc END AS fourth_latest 
        FROM UNNEST(rper.window_measures_history) AS hist
        WITH OFFSET as rnum
      )
    ) as trend_w_percentile_sc_in_gsc
FROM readanalytics.rpt_sc_rac_period rper
WHERE rper.assessment_academic_period_id IS NOT NULL
) AS trend
WHERE rper.assessment_academic_period_id = trend.assessment_academic_period_id
	AND rper.study_circle_id = trend.study_circle_id
	AND rper.dim_ra_cube_id = trend.dim_ra_cube_id
  AND rper.assessment_academic_period_id IS NOT NULL