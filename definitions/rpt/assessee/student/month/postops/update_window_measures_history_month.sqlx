config {
  type: "operations",
  dependencies: ["update_window_attr_derived_stu_month"]
}
SET @@query_label =
  "step:update_window_measures_history_month, " ||
  "batch_id: ${dataform.projectConfig.vars.batch_id}";

UPDATE `readanalytics.rpt_sc_stu_rac_month` rmon
SET rmon.window_measures_history = rmonmh.window_measures_history
FROM 
(SELECT rmon.student_id 
	, rmon.study_circle_id 
	, rmon.dim_ra_cube_id 
	, rmon.assessment_month 
  ,ARRAY_AGG(STRUCT(
    assessment_month
  , w_rank_stu_in_sc
  , w_count_student_in_sc
  , w_rank_stu_in_gsc
  , w_count_student_in_gsc
  , (1 - w_rank_stu_in_gsc / w_count_student_in_gsc)*100 as percentile_gsc
   )  
	)  OVER (
		PARTITION BY
			rmon.student_id 
			, rmon.study_circle_id 
			, rmon.dim_ra_cube_id 
		ORDER BY rmon.assessment_month DESC
		RANGE BETWEEN CURRENT ROW AND UNBOUNDED FOLLOWING
	) AS window_measures_history
FROM readanalytics.rpt_sc_stu_rac_month rmon
WHERE count_present_responses >0
   AND rmon.assessment_month>= DATE_TRUNC(DATE('${dataform.projectConfig.vars.batch_min_assessment_date}'),MONTH)
   AND rmon.assessment_month<= DATE_TRUNC(DATE('${dataform.projectConfig.vars.batch_max_assessment_date}'),MONTH)
) rmonmh

   WHERE 
   rmon.dim_ra_cube_id = rmonmh.dim_ra_cube_id
   AND rmon.study_circle_id = rmonmh.study_circle_id
   AND rmon.student_id = rmonmh.student_id 
   AND rmon.assessment_month = rmonmh.assessment_month
   AND rmon.assessment_month>= DATE_TRUNC(DATE('${dataform.projectConfig.vars.batch_min_assessment_date}'),MONTH)
   AND rmon.assessment_month<= DATE_TRUNC(DATE('${dataform.projectConfig.vars.batch_max_assessment_date}'),MONTH)
