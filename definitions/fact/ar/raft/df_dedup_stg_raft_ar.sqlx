config {
  type: "table",
  dependencies: "df_unnest_stg_raft_ar"
}
pre_operations {
    SET @@query_label =
      "step:df_dedup_stg_raft_ar, " ||
      "batch_id: ${dataform.projectConfig.vars.batch_id}";
}
-- df_dedup_stg_raft_ap
WITH stg_unnest_deduped AS (
  SELECT stg_unnest.*
    , ROW_NUMBER()
      OVER (
        PARTITION BY 
          stg_unnest.assessment_id
          , stg_unnest.student_id
          , stg_unnest.question_id
        ORDER BY stg_unnest.ts ASC
      ) AS row_num
  FROM readanalytics.df_unnest_stg_raft_ar stg_unnest
  LEFT JOIN `readanalytics.fact_ar` fact_ar 
    ON stg_unnest.assessment_id = fact_ar.assessment_id
    AND stg_unnest.student_id = fact_ar.student_id
    AND stg_unnest.question_id = fact_ar.question_id
  WHERE fact_ar.assessment_id IS NULL --remove duplication with fact_ar table
)
SELECT *
FROM stg_unnest_deduped
WHERE row_num = 1 --remove duplication within df_unnest_stg_raft_ap table