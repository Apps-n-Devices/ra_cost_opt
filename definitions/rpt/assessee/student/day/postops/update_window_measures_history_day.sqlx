config {
  type: "operations",
  dependencies: ["update_window_attr_derived_stu_day"]
}
SET @@query_label =
  "step:update_window_measures_history_day, " ||
  "batch_id: ${dataform.projectConfig.vars.batch_id}";

UPDATE readanalytics.rpt_sc_stu_rac_day rday
SET rday.window_measures_history = rday_percentile_hist.window_measures_history
FROM (
SELECT rday.student_id 
	, rday.study_circle_id 
	, rday.dim_ra_cube_id 
	, rday.assessment_date 
  ,ARRAY_AGG(STRUCT(
    assessment_date
  , w_rank_stu_in_sc
  , w_count_student_in_sc
  , w_rank_stu_in_gsc
  , w_count_student_in_gsc
  , (1 - w_rank_stu_in_gsc / w_count_student_in_gsc)*100 as percentile_gsc
  )  
	)  OVER (
		PARTITION BY 		
			rday.student_id 
			, rday.study_circle_id 
			, rday.dim_ra_cube_id 
		ORDER BY rday.assessment_date DESC
		RANGE BETWEEN CURRENT ROW AND UNBOUNDED FOLLOWING
	) AS window_measures_history
FROM readanalytics.rpt_sc_stu_rac_day rday
    WHERE rday.assessment_date >=(SELECT tbl.min_assessment_date FROM `readanalytics.df_vars_dimkeys_stg_raft_ar` tbl)
    AND rday.assessment_date<=(SELECT tbl.max_assessment_date FROM `readanalytics.df_vars_dimkeys_stg_raft_ar` tbl) 
    AND rday.count_present_responses >0
    AND rday.assessment_date>= DATE('${dataform.projectConfig.vars.batch_min_assessment_date}')
    AND rday.assessment_date<= DATE('${dataform.projectConfig.vars.batch_max_assessment_date}')
) rday_percentile_hist
WHERE 
    rday.dim_ra_cube_id = rday_percentile_hist.dim_ra_cube_id
    AND rday.student_id = rday_percentile_hist.student_id
    AND rday.study_circle_id = rday_percentile_hist.study_circle_id
    AND rday.assessment_date = rday_percentile_hist.assessment_date
    AND rday.assessment_date>= DATE('${dataform.projectConfig.vars.batch_min_assessment_date}')
    AND rday.assessment_date<= DATE('${dataform.projectConfig.vars.batch_max_assessment_date}')