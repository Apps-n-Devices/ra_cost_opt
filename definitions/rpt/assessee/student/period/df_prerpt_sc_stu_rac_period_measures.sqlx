config {
  type: "table"
}
pre_operations {
    SET @@query_label =
      "step:df_prerpt_sc_stu_rac_period_measures, " ||
      "batch_id: ${dataform.projectConfig.vars.batch_id}";
}
SELECT 
    -- --primary keys      
      stp.study_circle_id
    , stp.student_id
    , stp.dim_ra_cube_id
    , stp.assessment_academic_period_id 
 
    -- Performance measures
    , CAST(readanalytics.safe_add(stp.sum_response_time_taken,rstp.sum_response_time_taken) AS INT64) AS sum_response_time_taken
    , readanalytics.safe_add(stp.sum_response_main_score,rstp.sum_response_main_score) AS sum_response_main_score
    , readanalytics.safe_add(stp.sum_response_accuracy_score,rstp.sum_response_accuracy_score) AS sum_response_accuracy_score
    , readanalytics.safe_add(stp.sum_response_speed_score,rstp.sum_response_speed_score) AS sum_response_speed_score
    , readanalytics.safe_add(stp.sum_response_difficulty_score,rstp.sum_response_difficulty_score) AS sum_response_difficulty_score
    , readanalytics.safe_add(stp.sum_response_velocity,rstp.sum_response_velocity) AS sum_response_velocity
    
   -- -- Count measures
    , CAST(readanalytics.safe_add(stp.count_total_responses,rstp.count_total_responses) AS INT64) AS count_total_responses
    , CAST(readanalytics.safe_add(stp.count_correct_responses,rstp.count_correct_responses) AS INT64) AS count_correct_responses
    , CAST(readanalytics.safe_add(stp.count_incorrect_responses,rstp.count_incorrect_responses) AS INT64) AS count_incorrect_responses
    , CAST(readanalytics.safe_add(stp.count_near_correct_responses, rstp.count_near_correct_responses) AS INT64) AS count_near_correct_responses
    , CAST(readanalytics.safe_add(stp.count_not_answered_responses,rstp.count_not_answered_responses) AS INT64) AS count_not_answered_responses
    , CAST(readanalytics.safe_add(stp.count_absent_responses,rstp.count_absent_responses) AS INT64) AS count_absent_responses
    , CAST(readanalytics.safe_add(stp.count_present_responses,rstp.count_present_responses) AS INT64) AS count_present_responses

  -- Audit fields
   , stp.ts 
  
  -- history fields
  , ARRAY (
      SELECT AS STRUCT 
        assessment_month
      , SUM(sum_response_velocity) AS sum_response_velocity
      , SUM(sum_response_accuracy_score) AS sum_response_accuracy_score
      , SUM(sum_response_main_score) AS sum_response_main_score
      , SUM(sum_response_time_taken) AS sum_response_time_taken
      , SUM(count_present_responses) AS count_present_responses
      FROM
      (
        SELECT AS STRUCT * FROM UNNEST(stp.measures_history) as s
        UNION ALL
        SELECT AS STRUCT * FROM UNNEST(rstp.measures_history) as r
      )
      GROUP BY assessment_month
      ORDER BY assessment_month DESC
   ) AS measures_history

    -- --handraises
    , CAST(readanalytics.safe_add(stp.count_hand_raises,rstp.count_hand_raises) AS INT64) AS count_hand_raises

    -- --assessment and question history
      , ARRAY (
      SELECT AS STRUCT 
      assessment_id
      , question_id
      , SUM(count_total_responses) AS count_total_responses
      , SUM(count_correct_responses) AS count_correct_responses
      , SUM(count_incorrect_responses) AS count_incorrect_responses
      , SUM(count_near_correct_responses) AS count_near_correct_responses
      , SUM(count_not_answered_responses) AS count_not_answered_responses
      , SUM(count_absent_responses) AS count_absent_responses
      FROM
      (
        SELECT AS STRUCT * FROM UNNEST(stp.arr_assessment_questions) as s
        UNION ALL
        SELECT AS STRUCT * FROM UNNEST(rstp.arr_assessment_questions) as r
      ) 
      GROUP BY assessment_id,question_id
   ) AS arr_assessment_questions

  -- --postops
    , rstp.w_rank_stu_in_sc
    , rstp.w_rank_stu_in_gsc
    , rstp.w_count_student_in_sc
    , rstp.w_count_student_in_gsc
    , rstp.w_sum_response_velocity_sc
    , rstp.w_sum_response_velocity_gsc
    , rstp.w_sum_response_accuracy_score_gsc
    , rstp.w_sum_response_main_score_gsc
    , rstp.w_sum_response_time_taken_gsc
    , rstp.window_measures_history
    , rstp.trend_w_percentile_student_in_gsc
    , rstp.w_count_present_responses_sc
    , rstp.w_count_present_responses_gsc
    , rstp.w_avg_response_velocity_sc
    , rstp.w_avg_response_velocity_gsc
    , rstp.w_avg_response_accuracy_score_gsc
    , rstp.w_avg_response_main_score_gsc
    , rstp.w_avg_response_time_taken_gsc
    , rstp.w_percentile_student_gsc
    , rstp.w_count_answered_responses_gsc

FROM ${ref("df_sc_stu_rac_period")} stp
LEFT JOIN
  readanalytics.rpt_sc_stu_rac_period rstp
  ON 
  stp.dim_ra_cube_id = rstp.dim_ra_cube_id
  AND stp.student_id = rstp.student_id 
  AND stp.study_circle_id = rstp.study_circle_id
  AND stp.assessment_academic_period_id = rstp.assessment_academic_period_id
  AND rstp.assessment_academic_period_id IS NOT NULL


