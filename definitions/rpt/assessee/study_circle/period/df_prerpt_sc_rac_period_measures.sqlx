config {
  type: "table"
}
pre_operations {
    SET @@query_label =
      "step:df_prerpt_sc_rac_period, " ||
      "batch_id: ${dataform.projectConfig.vars.batch_id}";
}

SELECT 
   -- --primary keys
      scp.study_circle_id
    , scp.dim_ra_cube_id
    , scp.assessment_academic_period_id
  
   -- Performance measures
    , CAST(readanalytics.safe_add(scp.sum_response_time_taken,rscp.sum_response_time_taken) AS INT64) AS sum_response_time_taken
    , readanalytics.safe_add(scp.sum_response_main_score,rscp.sum_response_main_score) AS sum_response_main_score
    , readanalytics.safe_add(scp.sum_response_accuracy_score,rscp.sum_response_accuracy_score) AS sum_response_accuracy_score
    , readanalytics.safe_add(scp.sum_response_speed_score,rscp.sum_response_speed_score) AS sum_response_speed_score
    , readanalytics.safe_add(scp.sum_response_difficulty_score,rscp.sum_response_difficulty_score) AS sum_response_difficulty_score
    , readanalytics.safe_add(scp.sum_response_velocity,rscp.sum_response_velocity) AS sum_response_velocity
    
   -- -- Count measures
    , CAST(readanalytics.safe_add(scp.count_total_responses,rscp.count_total_responses) AS INT64) AS count_total_responses
    , CAST(readanalytics.safe_add(scp.count_correct_responses,rscp.count_correct_responses) AS INT64) AS count_correct_responses
    , CAST(readanalytics.safe_add(scp.count_incorrect_responses,rscp.count_incorrect_responses) AS INT64) AS count_incorrect_responses
    , CAST(readanalytics.safe_add(scp.count_near_correct_responses, rscp.count_near_correct_responses) AS INT64) AS count_near_correct_responses
    , CAST(readanalytics.safe_add(scp.count_not_answered_responses,rscp.count_not_answered_responses) AS INT64) AS count_not_answered_responses
    , CAST(readanalytics.safe_add(scp.count_absent_responses,rscp.count_absent_responses) AS INT64) AS count_absent_responses
    , CAST(readanalytics.safe_add(scp.count_present_responses,rscp.count_present_responses) AS INT64) AS count_present_responses
    , CAST(readanalytics.safe_add(scp.count_hand_raises,rscp.count_hand_raises) AS INT64) AS count_hand_raises

     -- --measures history
    , ARRAY (
      SELECT AS STRUCT 
        assessment_month
      , SUM(sum_response_velocity) AS sum_response_velocity
      , SUM(sum_response_accuracy_score) AS sum_response_accuracy_score
      , SUM(sum_response_main_score) AS sum_response_main_score
      , SUM(sum_response_time_taken) AS sum_response_time_taken
      , SUM(count_present_responses) AS count_present_responses
      FROM
      (
        SELECT AS STRUCT * FROM UNNEST(scp.measures_history) as s
        UNION ALL
        SELECT AS STRUCT * FROM UNNEST(rscp.measures_history) as r
      )
      GROUP BY assessment_month
      ORDER BY assessment_month DESC
   ) AS measures_history

    -- --assessment and question history
      , ARRAY (
      SELECT AS STRUCT 
      assessment_id
      , question_id
      , SUM(count_total_responses) AS count_total_responses
      , SUM(count_correct_responses) AS count_correct_responses
      , SUM(count_incorrect_responses) AS count_incorrect_responses
      , SUM(count_near_correct_responses) AS count_near_correct_responses
      , SUM(count_not_answered_responses) AS count_not_answered_responses
      , SUM(count_absent_responses) AS count_absent_responses
      FROM
      (
        SELECT AS STRUCT * FROM UNNEST(scp.arr_assessment_questions) as s
        UNION ALL
        SELECT AS STRUCT * FROM UNNEST(rscp.arr_assessment_questions) as r
      )
      GROUP BY assessment_id,question_id
   ) AS arr_assessment_questions
    
    -- --array assessment
    , ARRAY (
      SELECT AS STRUCT 
      assessment_id
      FROM
      (
        SELECT AS STRUCT * FROM UNNEST(scp.arr_assessments) as s
        UNION ALL
        SELECT AS STRUCT assessment_id FROM UNNEST(rscp.arr_assessments) as r
      )
      GROUP BY assessment_id
    ) AS arr_assessments

  -- Audit fields
   , scp.ts 

   --postops
   , rscp.w_rank_sc_in_gsc
   , rscp.w_count_sc_in_gsc
   , rscp.w_sum_response_velocity_gsc
   , rscp.w_sum_response_accuracy_score_gsc
   , rscp.w_sum_response_main_score_gsc
   , rscp.w_sum_response_time_taken_gsc
   , rscp.w_count_present_responses_gsc
   , rscp.w_count_answered_responses_gsc
   , rscp.w_avg_response_velocity_gsc
   , rscp.w_avg_response_accuracy_score_gsc
   , rscp.w_avg_response_main_score_gsc
   , rscp.w_avg_response_time_taken_gsc
   , rscp.w_percentile_sc_gsc
   , rscp.window_measures_history
   , rscp.trend_w_percentile_sc_in_gsc

FROM ${ref("df_sc_rac_period")} scp
LEFT JOIN
  readanalytics.rpt_sc_rac_period rscp
  ON 
  scp.dim_ra_cube_id = rscp.dim_ra_cube_id
  AND scp.study_circle_id = rscp.study_circle_id
  AND scp.assessment_academic_period_id = rscp.assessment_academic_period_id
  AND rscp.assessment_academic_period_id IS NOT NULL

